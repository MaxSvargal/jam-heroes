package DropTable

import HashList
import LinkedList
import StringUtils
import ObjectIds

public class DropTable
	HashList<int> items = new HashList<int>()

	function addDropItem(int itemId)
		items.add(itemId)

	function addDropNothing()
		items.add(0)

	function getRandomItem() returns int
		let i = GetRandomInt(0, items.size() - 1)
		return items.get(i)
		
	function dropRandomItemAtPos(vec2 pos) returns item
		let itemId = getRandomItem()
		if itemId != 0
			return createItem(itemId, pos)
		else
			return null

	function asString() returns string
		let list = new LinkedList<int>
		let iterator = items.iterator()
		for i from iterator
			list.add(i)
		let result = list.joinBy((int i) -> i.toRawCode(), ",")
		destroy list
		return result