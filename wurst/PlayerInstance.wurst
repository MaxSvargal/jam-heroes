package PlayerInstance

import ClosureEvents
import ClosureTimers
import UnitIds
import InstantDummyCaster
import Orders

import GroupUnitsTowers
import HeroesList
import PlayersSpawnPoints

import Laboratory
import Tavern
import Selector
import UnitBuildPlaceSmall
import UnitBuildPlaceLarge

import ArmorRecieptsShop
import ArmorShop
import MagicArmorShop
import PotionsShop
import RareRecieptsShop
import WeaponShop

import RecoveryDebuff
import MoveToCastle
import initlater TheEscape

public class PlayerInstance
	protected constant player p
	group groupInBattle
	unit hero
	unit heroOnMap
	rect castleRect
	vec2 selectHeroArea
	vec2 onGlobalMapPos
	vec2 castleHeroPoint
	
	construct(player pl)
		p = pl
		p.addGold(50)
		// p.addGold(2000)
		// p.addLumber(2000)
		initGroupBuildingPlaces()
		createUnit(p, SELECTOR_ID, gg_rct_InitialPos.getCenter(), angle(200))
		p.setCameraPosition(gg_rct_InitialPos.getCenter())

		onGlobalMapPos = getRandomSpawnPoint()

		EventListener.add(EVENT_PLAYER_UNIT_SELL) ->
			onHeroBought(GetSellingUnit(), GetSoldUnit())

		EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST) ->
			if GetTriggerPlayer() == p
				let abilId = GetSpellAbilityId()
				if abilId == MOVE_TO_CASTLE_ID
					moveToCastle()
				if abilId == MOVE_TO_GLOBAL_MAP_ID
					moveToGlobalMap()

		// Debuff logic
		EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
			addDebuffToBuildOnUnitDeath(GetTriggerUnit())

	function setVisibleRect(rect rectForVisible)
		CreateFogModifierRectBJ(true, p, FOG_OF_WAR_VISIBLE, rectForVisible)

	function setPositionOfHeroInCastle(rect pos)
		castleHeroPoint = pos.getCenter()

	function setCastleRect(rect castleRect)
		this.castleRect = castleRect

	function createShops()
		// 0deg is right
		// 90deg is top
		// 180deg is left
		// 270deg is bottom
		// <- x ^ y
		let castleCenter = castleRect.getCenter()
		let posTopRight0 = vec2(castleRect.getMaxX(), castleRect.getMaxY()).moveTowards(castleCenter, 300)
		let posTopRight1 = vec2(castleRect.getMaxX() - 300, castleRect.getMaxY()).moveTowards(castleCenter, 300)
		let posTopRight2 = vec2(castleRect.getMaxX(), castleRect.getMaxY() - 300).moveTowards(castleRect.getCenter(), 300)
		let posBtmRight0 = vec2(castleRect.getMaxX(), castleRect.getMinY()).moveTowards(castleRect.getCenter(), 300)
		let posBtmRight1 = vec2(castleRect.getMaxX() - 300, castleRect.getMinY()).moveTowards(castleRect.getCenter(), 300)
		let posBrmRight2 = vec2(castleRect.getMaxX(), castleRect.getMinY() + 300).moveTowards(castleRect.getCenter(), 300)
		let posTopLeft0 = vec2(castleRect.getMinX(), castleRect.getMaxY()).moveTowards(castleRect.getCenter(), 300)

		createUnit(p, ARMOR_SHOP_ID, posTopRight1, posTopRight1.angleTo(castleCenter))
		createUnit(p, MAGIC_ARMOR_SHOP_ID, posTopRight2, posTopRight2.angleTo(castleCenter))
		createUnit(p, ARMOR_RECIEPTS_SHOP_ID, posTopRight0, posTopRight0.angleTo(castleCenter))

		createUnit(p, WEAPON_SHOP_ID, posBrmRight2, posBrmRight2.angleTo(castleCenter))
		createUnit(p, POTIONS_SHOP_ID, posBtmRight1, posBtmRight1.angleTo(castleCenter))
		createUnit(p, RARE_RECIEPTS_SHOP_ID, posBtmRight0, posBtmRight0.angleTo(castleCenter))

		createUnit(p, LABORATORY_ID, posTopLeft0, posTopLeft0.angleTo(castleCenter))

	function getId() returns int
		return p.getId()

	private function addDebuffToBuildOnUnitDeath(unit u)
		if groupInBattle.contains(u)
		and groupUnitsTowers.getUnitToBuildingRelations().has(u.getTypeId())
			let buildingId = groupUnitsTowers.getUnitToBuildingRelations().get(u.getTypeId())
			print("unit contains!" + buildingId.toString())
			print(GetUnitsOfPlayerAndTypeId(p, buildingId).size())
			let building = GetUnitsOfPlayerAndTypeId(p, buildingId).next()
			print("building: " + building.getName())
			building.addAbility(RECOVERY_DEBUFF_ABILITY_ID)

	private function onHeroBought(unit selling, unit sold)
		if selling.getTypeId() == TAVERN_ID and sold.getOwner() == p
			hero = sold
			// DEV
			// hero.setLevel(5, true)
			// createUnit(players[1], sold.getTypeId(), castleHeroPoint, angle(0))
			hero
				..setPos(castleHeroPoint)
				..addAbility(MOVE_TO_GLOBAL_MAP_ID)
			listenForHeroDies()
			p.selectSingle(hero)
			p.setCameraPosition(castleHeroPoint)
			createHeroOnMap(onGlobalMapPos)

	private function createHeroOnMap(vec2 pos)
		let forMapHero = heroesOnMapAndInBattleRelations.get(hero.getTypeId())
		heroOnMap = createUnit(p, forMapHero, pos, angle(220))
		heroOnMap.addAbility(MOVE_TO_CASTLE_ID)
		pauseHeroOnMap(true)
		// TODO: DEV
		// createItem(TREASURE_ID, pos)

	private function moveToCastle()
		pauseHeroOnMap(true)
		hero
			..setPos(castleHeroPoint)
			..show()
			..addAbility(MOVE_TO_GLOBAL_MAP_ID)
			..removeAbility(THE_ESCAPE_ID)
		p.setCameraPosition(castleHeroPoint)
		heroOnMap.hide()
		onGlobalMapPos = heroOnMap.getPos()
		p.selectSingle(hero)

	private function moveToGlobalMap()
		pauseHeroOnMap(false)
		hero
			..hide()
			..removeAbility(MOVE_TO_GLOBAL_MAP_ID)
		p.setCameraPosition(onGlobalMapPos)
		heroOnMap.show()
		p.selectSingle(heroOnMap)
		heroOnMap.setInvulnerable(true)
		doAfter(3, () -> heroOnMap.setInvulnerable(false))

	function createGroupAtPos(vec2 pos) returns group
		let allUnits = GetUnitsOfPlayerAll(p)
		let createdGroup = CreateGroup()
		for u in allUnits
			let t = u.getTypeId()
			let buildingToUnitRelations = groupUnitsTowers.getBuildingToUnitRelations()
			if buildingToUnitRelations.has(t)
				let uId = buildingToUnitRelations.get(t)
				let nu = createUnit(p, uId, pos, angle(0))
				p.select(nu)
				createdGroup.addUnit(nu)
		return createdGroup

	function startBattleAt(vec2 pos)
		pauseHeroOnMap(true)
		onGlobalMapPos = heroOnMap.getPos()
		hero
			..setPos(pos)
			..addAbility(THE_ESCAPE_ID)
			..setAbilityCooldown(THE_ESCAPE_ID, 1, 60)
			..show()
		p
			..setCameraPosition(pos)
			..selectSingle(hero)
		groupInBattle = createGroupAtPos(pos)

	function battleVictory()
		pauseHeroOnMap(false)
		hero.queueAnimation("Cast")
		moveToGlobalMap()
		p.addLumber(1)
		for u in groupInBattle
			u.remove()

	function battleDefeat()
		moveToCastle()
		for u in groupInBattle
			u.remove()

	function pauseHeroOnMap(boolean status)
		heroOnMap.setInvulnerable(status)
		if status
			heroOnMap.pause()
		else
			heroOnMap.unpause()

	private function initGroupBuildingPlaces()
		let small = GetUnitsOfPlayerAndTypeId(p, UnitIds.circleofpower)
		for sm from small
			let pos = sm.getPos()
			sm.remove()
			createUnit(p, UNIT_BUILD_PLACE_SMALL_ID, pos, angle(0))
		small.destr()

		let large = GetUnitsOfPlayerAndTypeId(p, UnitIds.circleofpowerlarge)
		for lg from large
			let pos = lg.getPos()
			lg.remove()
			createUnit(p, UNIT_BUILD_PLACE_LARGE_ID, pos, angle(0))
		large.destr()

	function isGroupAlive() returns bool
		var isAnyAlive = false
		for u in groupInBattle
			if u.isAlive()
				isAnyAlive = true
		return isAnyAlive

	function isHeroAlive() returns bool
		return hero.isAlive()

	function isAllGroupAlive() returns bool
		return isGroupAlive() or isHeroAlive()

	function isAllyOf(player pl) returns bool
		return p.isAllyOf(pl)

	function getPlayer() returns player
		return p

	function listenForHeroDies()
		EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
			if GetTriggerUnit() == hero
				let t = CreateTimer()
				let td = CreateTimerDialog(t)
					..setTitle("Hero revive")
					..setRealTimeRemaining(10)
					..display(true)
				doAfter(10) ->
					t.destr()
					td.destr()
					hero
						..revive(castleHeroPoint, true)
						..removeAbility(MOVE_TO_CASTLE_ID)
						..addAbility(MOVE_TO_GLOBAL_MAP_ID)
					p.setCameraQuickPosition(hero.getPos())
					p.selectSingle(hero)

			if GetTriggerUnit() == heroOnMap
				createHeroOnMap(GetTriggerUnit().getPos())
				pauseHeroOnMap(false)

	function moveToTreasury() // TODO: Use it!
		hero.setPos(gg_rct_Player0Treasury.getCenter())