package BattlesController

import Battle
import CreepsGroup
import HashList
import ClosureTimers
import Players

class BattlesController
	constant activeBattles = new HashList<Battle>()

	construct()
		listenForBattlesStates()

	function add(Battle battle)
		activeBattles.add(battle)

	function listenForBattlesStates()
		doPeriodically(2) cb ->
			let battlesIterator = activeBattles.iterator()
			for battle from battlesIterator
				if battle.playersInBattle.size() == 0
					activeBattles.remove(battle)
					destroy battle
				else
					let playersIterator = battle.playersInBattle.iterator()
					for p from playersIterator
						// When all players is allies, just victory all
						if Players.isAllPlayersAllies(battle.playersInBattle)
						and not battle.creepsGroup.isGroupAlive()
							p.battleVictory()
							battle.exitPlayerFromBattle(p)
							// TODO: bug? That ally player will be an owner?
							// battle.entityBattleFor.defeat()
							// TODO: drop items on the map and hero needs to move to exit
							// TODO: move method to player instead of droptable?
							battle.dropRandomItemAtPos(p.castleHeroPoint)
							battle.creepsDefeat(p.getPlayer())
							//dropRandomItemAtPos(campGroup.warlord.getPos())
						// When enemies players in battle
						else if not p.isAllGroupAlive()
							// player defeat
							p.battleDefeat()
							battle.exitPlayerFromBattle(p)
					playersIterator.close()
			battlesIterator.close()

public constant battlesController = new BattlesController()